
#Область ОбработчикиСобытийФормы

#КонецОбласти

#Область ОбработкаКомандФормы

&НаКлиенте
Процедура ВыгрузитьДанные(Команда)
	
	ВыгрузитьДанныеНаСервере();
	
	Сообщить("Выгрузка окончена");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКаталогДляВыгрузки(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	Если Диалог.Выбрать() Тогда
		Каталог = Диалог.Каталог;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФайлДляЗагрузки(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	Если Диалог.Выбрать() Тогда
		ФайлДляЗагрузки = Диалог.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры  

&НаКлиенте
Процедура ЗагрузитьДанные(Команда)
	
	ЗагрузитьДанныеНаСервере();
	
	Сообщить("Загрузка окончена");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ВыгрузитьДанныеНаСервере()
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(Каталог + "/ФайлДанныхСтолото.xml", "UTF-8");
	
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента("_1CV8DtUD", "http://www.1c.ru/V8/1CV8DtUD/");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("V8Exch", "http://www.1c.ru/V8/1CV8DtUD/");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("core", "http://v8.1c.ru/data");
	
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("v8", "http://v8.1c.ru/8.1/data/enterprise/current-config");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xs", "http://www.w3.org/2001/XMLSchema");
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("V8Exch:Data");
	
	ВыгрузитьРегистрПоследнийНомерТиража_7_49(ЗаписьXML);
 	ВыгрузитьРегистрКомбинацииЧисел_7_49(ЗаписьXML);
	
	ЗаписьXML.ЗаписатьКонецЭлемента(); // V8Exc:Data
	ЗаписьXML.ЗаписатьКонецЭлемента(); // V8Exc:_1CV8DtUD
	
	ЗаписьXML.Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ВыгрузитьРегистрПоследнийНомерТиража_7_49(ЗаписьXML)
	
	НаборЗаписей = РегистрыСведений.ПоследнийНомерТиража_7_49.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, НаборЗаписей);

КонецПроцедуры                    

&НаСервере
Процедура ВыгрузитьРегистрКомбинацииЧисел_7_49(ЗаписьXML)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КомбинацииЧисел_7_49.НомерТиража КАК НомерТиража,
		|	КомбинацииЧисел_7_49.Число1 КАК Число1,
		|	КомбинацииЧисел_7_49.Число2 КАК Число2,
		|	КомбинацииЧисел_7_49.Число3 КАК Число3,
		|	КомбинацииЧисел_7_49.Число4 КАК Число4,
		|	КомбинацииЧисел_7_49.Число5 КАК Число5,
		|	КомбинацииЧисел_7_49.Число6 КАК Число6,
		|	КомбинацииЧисел_7_49.Число7 КАК Число7
		|ИЗ
		|	РегистрСведений.КомбинацииЧисел_7_49 КАК КомбинацииЧисел_7_49";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаЗапись = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаЗапись.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.КомбинацииЧисел_7_49.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.НомерТиража.Установить(ВыборкаЗапись.НомерТиража);
		
		Для Сч = 1 По 7 Цикл
			НаборЗаписей.Отбор["Число" + Сч].Установить(ВыборкаЗапись["Число" + Сч]);
		КонецЦикла;
		
		НаборЗаписей.Прочитать();
		
		СериализаторXDTO.ЗаписатьXML(ЗаписьXML, НаборЗаписей);
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеНаСервере()
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ФайлДляЗагрузки);
	
	ЧтениеXML.Прочитать();
	ЧтениеXML.Прочитать();
	
	// Чтение и запись в ИБ записанных в выгрузке объектов.
	Если Не ЧтениеXML.Прочитать() Тогда 
		
		Сообщить(НСтр("ru = 'Неверный формат файла выгрузки'"));
		Возврат;
		
	КонецЕсли;
	
	Пока СериализаторXDTO.ВозможностьЧтенияXML(ЧтениеXML) Цикл
		
		ЗаписанноеЗначение = СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
		ЗаписанноеЗначение.Записать();
		
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	
КонецПроцедуры

#КонецОбласти

